import os
import pandas as pd
from os.path import join

"""
Update config.yaml
Ensure that the dorado_exec path points to the dorado executable.
Run 'snakemake --scheduler greedy --cores 32 --use-conda --profile mqsub-lyra --configfile config.yaml'
"""

dorado = config["dorado_exec"]
outfolder = config["output_dir"]
models_directory = config["models_directory"]

# Create necessary directories
os.makedirs(join(outfolder,'logs'), exist_ok=True)
os.makedirs(join(outfolder,'basecalled'), exist_ok=True)

# Read POD5 file list
pod5_files = pd.DataFrame(columns=["pod_id", "filepath"])
with open(config["pod5_sample_list"]) as r:
    for filepath in r:
        pod5_files = pd.concat([pod5_files, pd.DataFrame({"pod_id": [os.path.basename(filepath.strip())], "filepath": [filepath.strip()]})])

wildcard_constraints:
    podfile = r".+\.pod5"  # Ensure the wildcard matches valid POD5 filenames

rule all:
    input:
        join(outfolder, "demux.done")

rule dorado_basecaller:
    input:
        pod5_file = lambda wildcards: pod5_files[pod5_files["pod_id"] == wildcards.podfile].iloc[0]["filepath"]
    params:
        model = "sup",
        base_mods = "5mC_5hmC 6mA",
        kit_name = "SQK-NBD114-24"
    output:
        join(outfolder, "basecalled", "{podfile}.bam")
    log:
        join(outfolder, "logs", "{podfile}.log")
    threads: 6
    resources:
        mem_mb=64*1024, # Having (possibly unrelated problems) using 8G
        gpus=1,
        runtime=24*60*2
    shell:
        "bash -c 'nvidia-smi 1>&2 && {dorado} basecaller {params.model} {input.pod5_file} --modified-bases {params.base_mods} --kit-name {params.kit_name} --models-directory {models_directory} > {output}' 2> {log}"

rule cat_bamfiles:
    input:
        expand(join(outfolder, "basecalled/{pod_id}.bam"), pod_id=pod5_files["pod_id"])
    output:
        join(outfolder, "basecalls.bam")
    conda:
        "envs/samtools.yml"
    log:
        join(outfolder, "logs", "cat_bamfiles.log")
    resources:
        mem_mb=8*1024,
        runtime=12*60
    shell:
        "samtools merge {output} {input} 2> {log}"

rule dorado_demux:
    input:
        join(outfolder, "basecalls.bam")
    params:
        options = "--no-classify",
    output:
        outfolder = directory(join(outfolder, "final_bamfiles")),
        done = touch(join(outfolder, "demux.done"))
    log:
        join(outfolder, "logs", "demux.log")
    threads: 1
    resources:
        mem_mb=8*1024, # guess work
        runtime=12*60, # 12 hours - more than enough I think
        gpus=1, # Possible it might work in CPU mode, but doesn't at least of the box.
    shell:
        "{dorado} demux {input} {params.options} --output-dir {output.outfolder} 2> {log}"